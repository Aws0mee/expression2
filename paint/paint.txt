@name paint

#[ paint
    Hook up a digital screen to it and draw.
    Type # in chat to pick a color of the pixel you are looking at.
    Type a hex color code with # in the chat to select that as your color.
    
    Feel free to use this code as you
    please.
    
    https://ko-fi.com/meowlan
    https://github.com/Meowlan
]#


@inputs DS:wirelink
@persist Res PixelSize:vector2 G:gtable Interactors:table Holders:table
#include "digitalscreen"

Res = 128

G = gTable("paint")

local T = G:toTable()

if (!T:exists("users")) { G["users", table] = table()}
if (!T:exists("pixels")) { G["pixels", table] = table()}
if (!T:exists("hue")) { G["hue", number] = 0}

function wirelink:setPixel(Pos:vector2, Color:vector) {
    This[(Pos:x() - 1) * Res + (Pos:y() - 1)] = rgb2digi(Color, 2)        
}

function number wirelink:getPixel(Pos:vector2) {
    return This[(Pos:x() - 1) * Res + (Pos:y() - 1)]      
}

if ((first() | dupefinished() | ~DS) & ->DS) {
    #DS:clear()
    DS:setColorMode(2)
    
    Interactors = table()
    Holders = table()
    
    DS:setWidth(Res)
    DS:setHeight(Res)
    
    local Size = DS:entity():aabbSize()
    PixelSize3 = Size / (Res)
    PixelSize = vec2(PixelSize3:x(), PixelSize3:y())
    
    #holoCreate(1)
    #holoScaleUnits(1, vec(PixelSize:x(), PixelSize:y(), 1))
    #holoAng(1, DS:entity():angles())
    #holoMaterial(1, "models/wireframe")
    #holoParent(1, DS:entity())
    
    for (I=1, Res) {
        DS:setPixel(vec2(Res - 2, I), hsv2rgb(360 * (I/Res), 1, 1))
        DS:setPixel(vec2(Res, I), hsv2rgb(0, 0, I/Res))
    }
    
    for (I=1, Res / 2) {
        DS:setPixel(vec2(Res - 1, I + (Res / 2)), hsv2rgb(G["hue", number], 1 - (I / (Res / 2)), 1))
        DS:setPixel(vec2(Res - 1, I), hsv2rgb(G["hue", number], 1, I / (Res / 2)))
    }
}

function vector2 worldToScreen(Pos:vector) {
    local Pos3 = DS:entity():toLocal(Pos)
    local Pos2 = vec2(Pos3)
    
    return round((Pos2 / PixelSize) + vec2(0.5))
}

function vector2 vector2:translateCoords() {
    return This + vec2(Res/2)
}

function string componentToHex(C) {
    local Hex = C:toString(16)
    return Hex:length() == 1 ? "0" + Hex : Hex
}

function string rgb2hex(Color:vector) {
    return "#" + componentToHex(Color:x()) + componentToHex(Color:y()) + componentToHex(Color:z())  
}

function vector hex2rgb(Color:string) {
    if (Color:length() != 7) { exit() }
    Color = Color:upper()
    
    local R = Color:sub(2, 3):toNumber(16)
    local G = Color:sub(4, 5):toNumber(16)
    local B = Color:sub(6, 7):toNumber(16)
    
    return vec(R, G, B)
}

function vector digi2rgb(Color:number) {
    local R = floor(Color / 65536) % 256
    local G = floor(Color / 256) % 256
    local B = Color % 256
    return vec(R, G, B)
}

function entity:pmE(String:string) {
    if (This == owner() | !This:isValid()) {
        print(String)
        return    
    }
    
    String = String:replace("\n", " ")
    
    concmd(format("sam pm \"%s\" \"{white [Paint}:lower_left_paintbrush:{white ]} %s\"", This:steamID64(), String))
}

function entity:pm(String:string) {
    This:pmE(format("{white %s}", String))
}

function entity:sendColor(Color:string) {
    This:pmE(format("{%s %s %s}", Color, toUnicodeChar(9608):repeat(5), Color))
}

#[event tick() {
    if (owner():aimEntity() != DS:entity()) {exit()} 
    
    local Pos = (worldToScreen(owner():aimPos()) - vec2(0.5)) * PixelSize

    holoPos(1, DS:entity():toWorld(vec(Pos:x(), Pos:y(), 1.5)))
    holoColor(1, SelectedColor)
}]#

event chat(E:entity, S:string, N:number) {
    array(N)
    
    if (E:aimEntity() != DS:entity()) { exit() } 
    local Pos = round(worldToScreen(E:aimPos()):translateCoords()) 
    
    if (!G["users", table]:exists(E:steamID64())) {
        G["users", table][E:steamID64(), table] = table( "color" = vec(255))
    }
    
    if (S:sub(1, 1) == "#" & S:length() == 7) {
        G["users", table][E:steamID64(), table]["color", vector] = hex2rgb(S)
        E:sendColor(S)
    } elseif (S:sub(1, 1) == "#" & S:length() == 1) {
        local Color = digi2rgb(DS:getPixel(Pos))
        local ColorS = rgb2hex(Color)

        G["users", table][E:steamID64(), table]["color", vector] = Color
        E:sendColor(ColorS)
    } elseif( S == "#who") {
        local String = G["pixels", table][Pos:toString(), string]
        E:pm(String:length() ? format("Pixel owned by %s", G["pixels", table][Pos:toString(), string]) : "Pixel not owned")
    } elseif( S == "#vip") {
        if (G["users", table][E:steamID64(), table]["vip", number]) {
            E:pm("You already own vip!")
            exit()
        }
        
        moneyRequest(E, 500000, "[Paint] VIP")
        
    } elseif( S == "#?" | S == "#help") {
        E:pm("Available commands:
#    - Selects color you are looking at.
#ffffff    - Selects specified hex color.
#who    - Returns owner of pixel.
#?    - Displays this page.
#help    - Displays this page.
#vip    - Buy vip for more features!")
    }
}

event keyPressed(E:entity, Key:string, State:number, Bind:string) {
    array(Key)
    
    if (E:aimEntity() != DS:entity()) { exit() }
    if (E:pos():distance(entity():pos()) > 350) { exit() }
    
    if (State) {
        Interactors[E:steamID64(), entity] = E
    } else {
        Interactors:unset(E:steamID64())
    }
    
    if((Bind == "use") & State) {
        local Pos = round(worldToScreen(E:aimPos()):translateCoords())
        if (!G["users", table]:exists(E:steamID64())) {
            G["users", table][E:steamID64(), table] = table( "color" = vec(255))
        }
        
        if (Pos:x() > Res - 3) {
            local Color = digi2rgb(DS:getPixel(Pos))
            local HSV = rgb2hsv(Color)
            
            G["hue", number] = HSV:x()
            
            for (I=1, Res / 2) {
                DS:setPixel(vec2(Res - 1, I + (Res / 2)), hsv2rgb(G["hue", number], 1 - (I / (Res / 2)), 1))
                DS:setPixel(vec2(Res - 1, I), hsv2rgb(G["hue", number], 1, I / (Res / 2)))
            }
            
            G["users", table][E:steamID64(), table]["color", vector] = Color
            local ColorS = rgb2hex(Color)
            E:sendColor(ColorS)
            
        } else {
            G["users", table][E:steamID64(), table]["pos", vector2] = Pos
            #moneyRequest(E, 1000, "1 Pixel $1k")
            DS:setPixel(Pos, G["users", table][E:steamID64(), table]["color", vector])
            G["pixels", table][Pos:toString(), string] = E:name()
        }
    }    
}

event tick() {
    foreach(SteamID:string, T:number = Holders) {
        if (T > 1) {
            Interactors[SteamID, entity]:pm("Would you like to be able to drag? Buy paint VIP for more featues! Type #vip while looking at the canvas.")
            Holders[SteamID, number] = -5
        }    
    }
    
    foreach (SteamID:string, E:entity = Interactors) {
        if (!E:keyUse()) { exit() } 
        
        if (E:aimEntity() != DS:entity()) { continue }
        if (E:pos():distance(entity():pos()) > 350) { continue }
        
        # check if player has vip
        if (!G["users", table][E:steamID64(), table]["vip", number]) { 
            Holders[E:steamID64(), number] = Holders[E:steamID64(), number] + tickInterval()
            continue
        }
        
        Holders:unset(E:steamID64())
        
        local Pos = round(worldToScreen(E:aimPos()):translateCoords())
        
        if (Pos:x() > Res - 3) { exit() }
        
        G["users", table][E:steamID64(), table]["pos", vector2] = Pos
        DS:setPixel(Pos, G["users", table][E:steamID64(), table]["color", vector])
        G["pixels", table][Pos:toString(), string] = E:name()
    }
}

if (moneyClk()) {
    local Title = moneyClkTitle()
    local E = moneyClkPlayer()
    
    if (Title == "[Paint] VIP") {
        G["users", table][E:steamID64(), table]["vip", number] = 1    
        E:pm("Succesfully bought vip!")
    }
    
    #DS:setPixel(Data["pos", vector2], Data["color", vector]) 
}
